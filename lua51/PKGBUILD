
pkgname=lua
pkgver=5.1.5
pkgrel=2
pkgdesc='A powerful light-weight programming language designed for extending applications.'
arch=(i386 x86_64)
license=(MIT)
url='http://www.lua.org/'
depends=(libedit)
source=(http://www.lua.org/ftp/$pkgname-$pkgver.tar.gz)
md5sums=(2e115fe26e435e33b0d5c022e4490567)

build() { 
    cd $srcdir/$pkgname-$pkgver
    make macosx
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make INSTALL_TOP=$pkgdir/Library/ArchMac install

    install -m755 -d $pkgdir/Library/ArchMac/lib/pkgconfig
    install -m644 etc/lua.pc $pkgdir/Library/ArchMac/lib/pkgconfig/lua.pc
    install -m755 -d $pkgdir/Library/ArchMac/doc/$pkgname
    install -m644 COPYRIGHT $pkgdir/Library/ArchMac/doc/$pkgname/LICENSE
}

